#!/usr/bin/env amber

import * from "std/env"
import * from "std/fs"
import * from "std/text"

let workdirs = [
    "~/user/dotfiles/.config",
    "~/user/projects"
]

fun is_tmux_running(): Bool {
    let result = true
    if not env_var_test("TMUX") {
        let tmux_pid = $ pgrep tmux $ failed {
            result = false
        }
    }
    return result
}

fun has_session(name: Text): Bool {
    let result = true
    silent $ tmux has-session -t={name} $ failed {
        result = false
    }
    return result;
}

fun normalize_workdirs(ref dirs: [Text]) {
    let home = trust env_var_get("HOME")
    for i, dir in dirs {
        if {
            starts_with(dir, "~/") {
                dirs[i] = replace(dir, "~", home)
            }
            starts_with(dir, "$HOME") {
                dirs[i] = replace(dir, "$HOME", home)
            }
        }
        if not dir_exists(dirs[i]): echo_error("Can not find directory: {dir}")
    }
}

main(args) {
    if not is_command("tmux"): echo_error("This script needs tmux to run, install it first")

    normalize_workdirs(workdirs)

    let selected = ""
    if len(args) > 1 {
        selected = args[1]
    } else {
        selected = $ find {workdirs} -mindepth 1 -maxdepth 1 -type d | fzf $ failed {
            exit 0
        }
    }

    let name = trust $ basename {selected} | tr . _ $

    if {
        not is_tmux_running() {
            trust $ tmux new-session -s {name} -c {selected} $
            exit 0
        }
        not has_session(name) {
            trust $ tmux new-session -ds {name} -c {selected} $
            exit 0
        }

    }

    trust $ tmux switch-client -t {name} $
}
